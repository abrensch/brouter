plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "btools.routingapp"

        versionCode 42
        versionName project.version

        resValue('string', 'app_version', defaultConfig.versionName)
        setProperty("archivesBaseName","BRouterApp." + defaultConfig.versionName)
    }

	if(project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            // this uses a file  ~/.gradle/gradle.properties
            // with content:
            // RELEASE_STORE_FILE={path to your keystore}
            // RELEASE_STORE_PASSWORD=*****
            // RELEASE_KEY_ALIAS=*****
            // RELEASE_KEY_PASSWORD=*****
            //
            release {
               // enable signingConfig in buildTypes to get a signed apk file
               storeFile file(RELEASE_STORE_FILE)
               storePassword RELEASE_STORE_PASSWORD
               keyAlias RELEASE_KEY_ALIAS
               keyPassword RELEASE_KEY_PASSWORD

               // Optional, specify signing versions used
               v1SigningEnabled true
               v2SigningEnabled true

           }
        }
	}
	
    buildTypes {
        release {
            minifyEnabled false
            debuggable false
			if(project.hasProperty("RELEASE_STORE_FILE")) {
				signingConfig signingConfigs.release
			}
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
			debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
	lintOptions {
		disable 'InvalidPackage'
		checkReleaseBuilds false //added this line to the build.gradle under the /android/app/build.gradle 
    }
	
	compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    flavorDimensions "api"
    productFlavors {
        api19 {
            dimension "api"

            minSdkVersion 10
            targetSdkVersion 19
        }
        api30 {
            dimension "api"

            minSdkVersion 19
            targetSdkVersion 30
        }
    }

}

dependencies {

    api30Implementation 'androidx.appcompat:appcompat:1.3.1'

    implementation project(':brouter-mapaccess')
    implementation project(':brouter-core')
    implementation project(':brouter-expressions')
    implementation project(':brouter-util')
	
}
