// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id 'me.qoomon.git-versioning' version '6.3.6'
}

gitVersioning.apply {
    refs {
        considerTagsOnBranches = true
        tag('v(?<version>.*)') {
            version = '${ref.version}'
        }
        branch('.+') {
            version = '${describe.tag.version}-${describe.distance}-${ref}-g${commit.short}'
        }
    }

    rev {
        version = '${commit}'
    }
}

allprojects {
    group 'org.btools'

    repositories {
        mavenCentral()
        google()
    }

    apply plugin: "maven-publish"
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/$System.env.REPO")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication)
        }
    }

    apply plugin: "checkstyle"
    apply plugin: "pmd"

    pmd {
        consoleOutput = true
        toolVersion = "6.51.0"
        rulesMinimumPriority = 5
        ruleSetFiles = files("${rootProject.rootDir}/config/pmd/pmd-ruleset.xml")
        ruleSets = []
        // ignoreFailures = true
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
